;SYSTAB.MAC
;860312 ALEX

INCLUDE 8KLISP.DEF

SYMB MACRO VALUE,CODE,PNAME
$LEN DEFL .&CODE-$
	DB $LEN
	DW VALUE,CODE##,NIL
	DW .&CODE
	DC PNAME
	DS (3 - (($-HEAP) AND 3) AND 3)
	DB $LEN
.&CODE:
	ENDM

	DSEG

;******************;
; SYSTEM VARIABLES ;
;******************;
T1::	DS 128			;ARITHMETIC BUFFERS AND FILE BUFFER FOR SAVE+LOAD
T2::	DS 128
LBP::	DW LBUF			;LINE BUFFER PTR
SHORT::	DB 1,0			;SHORT NUMBER BUFFER

SAVSYS::DB "8K"
MEMTOP::DW 0			;TOP OF MEMORY
HEAPTR::DW FREEMEM		;HEAP SPACE PTR
STKSAV::DW 0			;STACK SAVE
SINIT::	DW 0			;INITIAL STACK PTR
BASE::	DB 1,10			;NUMBER BASE
NXTCHR::DB ' '			;NEXT CHAR FOR READER
FLAG::	DB 0			;GENERAL PURPOSE FLAG
LEVEL::	DB 0			;READ-EVAL-LOOP LEVEL
IOCHAN::DW NIL			;I/O CHANNEL ADDRESS
TEMP::	DW 0			;TEMPORARY STORAGE
FBASE::	DW 0			;FRAME BASE
CBASE::	DW 0			;CATCH BASE
GSEED::	DB "G00000"		;GENSYM SEED
CELL0::	DW NIL,1100H	;NIL-CONS CELL ****
ZERO::	DB 84H,0		;ZERO MUST START AT OBJECT BOUNDARY! ****
FILMAX::DB 0			;MAXIMUM NUMBER OF FILES
STEPM::	DB 0			;STEPMODE
ONE::	DB 84H,1,1		;ONE MUST START AT OBJECT BOUNDARY! ****
LDFLG::	DB 0			;LOAD FLAG

;THE HEAP MUST START AT AN ADDRESS WHICH IS ONE MORE THAN A MULTIPLE OF 4
;(OBJECT BOUNDARY)
HEAP::
TSYM::
	SYMB TSYM,TRET,"t"
QUOTSYM::
	SYMB 0,QUOTE,"quote"
GCSYM::
	SYMB NIL,GC,"gc"
ALCSYM::
	SYMB NIL,ALLOC,"alloc"
INTSYM::
	SYMB TSYM,INTERN,"intern"
SSSYM::
	SYMB NIL,SS,"ss"
ATSYM::
	SYMB 0,AT_OP,"@"
STEPSYM::
	SYMB ZERO,STEP,"step"
PROTECT::
	SYMB 0,RUN,"run"
	SYMB 0,STOP,"stop"
	SYMB 0,BIOS,"bios"
	SYMB 0,BDOS,"bdos"
	SYMB 0,REVALO,"revalo"
	SYMB 0,EVAL,"eval"
	SYMB 0,APPLY,"apply"
	SYMB 0,SETQ,"setq"
	SYMB 0,SET,"set"
	SYMB 0,CHAR,"char"
	SYMB 0,ASCII,"ascii"
	SYMB 0,POP,"pop"
	SYMB 0,PUSH,"push"
	SYMB 0,SAVE,"save"
	SYMB 0,LOAD,"load"
	SYMB 0,REN,"ren"
	SYMB 0,ERA,"era"
	SYMB 0,DIR,"dir"
	SYMB 0,IN,"in"
	SYMB 0,CRLF,"cr"
	SYMB 0,SPC,"sp"
	SYMB 0,GETC,"getc"
	SYMB 0,PUTC,"putc"
	SYMB 0,PRIN1,"prin1"
	SYMB 0,PRINT,"print"
	SYMB 0,KEY,"key"
	SYMB 0,READ,"read"
	SYMB 0,SEEK,"seek"
	SYMB 0,WHERE,"where"
	SYMB 0,EOFP,"eofp"
	SYMB 0,CLOSE,"close"
	SYMB 0,CREATE,"create"
	SYMB 0,OPEN,"open"
	SYMB 0,MPCAN,"mapcan"
	SYMB 0,MPCAR,"mapcar"
	SYMB 0,MAPC,"mapc"
	SYMB 0,REMOB,"remob"
	SYMB 0,UNPACK,"unpack"
	SYMB 0,PACK,"pack"
	SYMB 0,GENSYM,"gensym"
	SYMB 0,REVERSE,"reverse"
	SYMB 0,ASSOC,"assoc"
	SYMB 0,DELETE,"delete"
	SYMB 0,MEMQ,"memq"
	SYMB 0,MEMBER,"member"
	SYMB 0,LENGTH,"length"
	SYMB 0,NCONC,"nconc"
	SYMB 0,APPEND,"append"
	SYMB 0,LIST,"list"
	SYMB 0,CONS,"cons"
	SYMB 0,RPLCD,"rplacd"
	SYMB 0,RPLCA,"rplaca"
	SYMB 0,NTHCDR,"nthcdr"
	SYMB 0,NTH,"nth"
	SYMB 0,CDDDR,"cdddr"
	SYMB 0,CDDAR,"cddar"
	SYMB 0,CDADR,"cdadr"
	SYMB 0,CDAAR,"cdaar"
	SYMB 0,CADDR,"caddr"
	SYMB 0,CADAR,"cadar"
	SYMB 0,CAADR,"caadr"
	SYMB 0,CAAAR,"caaar"
	SYMB 0,CDDR,"cddr"
	SYMB 0,CDAR,"cdar"
	SYMB 0,CADR,"cadr"
	SYMB 0,CAAR,"caar"
	SYMB 0,CDR,"cdr"
	SYMB 0,CAR,"car"
	SYMB 0,DEFUN,"de"
	SYMB 0,GETD,"getd"
	SYMB 0,PUTD,"putd"
	SYMB 0,GETPL,"getpl"
	SYMB 0,PUTPL,"putpl"
	SYMB 0,GET,"get"
	SYMB 0,PUT,"put"
	SYMB 0,MFREE,"mfree"
	SYMB 0,RADIX,"radix"
	SYMB 0,ABS,"abs"
	SYMB 0,MIN,"min"
	SYMB 0,MAX,"max"
	SYMB 0,DEC,"dec"
	SYMB 0,INC,"inc"
	SYMB 0,SQRT,"sqrt"
	SYMB 0,MODLS,"%"
	SYMB 0,HALF,"2/"
	SYMB 0,DOUBLE,"2*"
	SYMB 0,ONESUB,"1-"
	SYMB 0,ONEADD,"1+"
	SYMB 0,DIV,"/"
	SYMB 0,MULT,"*"
	SYMB 0,SUB,"-"
	SYMB 0,ADD,"+"
	SYMB 0,EQUAL,"equal"
	SYMB 0,C_EQ,"eq"
	SYMB 0,PAIR,"pairp"
	SYMB 0,ATOM,"atom"
	SYMB 0,NUMBER,"numberp"
	SYMB 0,BOUNDP,"boundp"
	SYMB 0,SYMBOL,"symbolp"
	SYMB 0,LESSP,"lessp"
	SYMB 0,ODDP,"oddp"
	SYMB 0,MINUSP,"minusp"
	SYMB 0,ZEROP,"zerop"
	SYMB 0,L_NOT,"not"
	SYMB 0,L_OR,"or"
	SYMB 0,L_AND,"and"
	SYMB 0,THROW,"throw"
	SYMB 0,CATCH,"catch"
	SYMB 0,RETURN,"return"
	SYMB 0,GO,"go"
	SYMB 0,EPROGN,"eprogn"
	SYMB 0,PROGN,"progn"
	SYMB 0,PROG2,"prog2"
	SYMB 0,PROG1,"prog1"
	SYMB 0,REPTN,"reptn"
	SYMB 0,UNTIL,"until"
	SYMB 0,WHILE,"while"
;; Removed undocumented do function due to size restrictions
;	SYMB 0,DO,"do"
	SYMB 0,IF,"if"
	SYMB 0,WHEN,"when"
	SYMB 0,UNLESS,"unless"
	SYMB 0,COND,"cond"

;LAST SYMBOL
	DB	16
	DW	0,OBLIST##,NIL,NIL
	DC	"oblist"
	DB	16

FREEMEM::

;******************;
;SINGLE BYTE TOOLS ;
;******************;
TOOLS::
	LD E,(HL)		;RST 08H: POP LIST IN HL INTO DE
	INC HL
	LD D,(HL)
	INC HL
	LD A,(HL)		;CDR IN HL (REVERSE)
	INC HL
	LD L,(HL)
	LD H,A
	RET

	DW NIL			;NIL AT NIL'S ADDRESS (0011H)
	DB HIGH(NIL)
	DB LOW(NIL)
	NOP
	NOP
	NOP

	LD A,(HL)			;RST 18H: FETCH WORD ADDRESSED BY HL
	INC HL
	LD H,(HL)
	LD L,A
	RET
	NOP
	NOP
	NOP

	LD A,H				;RST 20H: COMPARE DE AND HL
	CP D
	RET NZ
	LD A,L
	CP E
	RET
	NOP
	NOP

	LD A,H				;RST 28H: COMPARE BC AND HL
	CP B
	RET NZ
	LD A,L
	CP C
	RET

	END
